import entrenador.*
import Golondrina.*
import Ciudad.*

describe "Entrenador test" {
	
	var mdp
	var buenosAires
	var dolores
	var pepita
	var pepito
	var papo

	fixture {
		mdp = new Ciudad(kilometroEnRuta = 400)
		buenosAires = new Ciudad(kilometroEnRuta = 0)
		dolores = new Ciudad(kilometroEnRuta = 200)
		
		pepita = new Golondrina(energia = 100, ciudadActual = buenosAires, diarioDeViaje = [mdp, buenosAires])
		pepito = new Golondrina(energia =  50, ciudadActual = dolores, diarioDeViaje = [mdp, buenosAires, dolores])
		papo   = new Golondrina(energia = 200, ciudadActual = mdp, diarioDeViaje = [mdp])
		
		carlos.agregarAve(pepita)
	}
	
	test "Carlos arranca solo con pepita" {
		assert.equals(#{pepita}, carlos.aves())
	}
	
	test "Carlos agrega a papo correctamente a su equipo" {
		carlos.agregarAve(papo)
		assert.that(carlos.aves().contains(papo))
	}
	
	test "Carlos no esta listo para competir ya que pepita arranca con 100 de energia"{
		assert.notThat(carlos.estaListoParaCompetir())
	}
	
	test "Carlos esta listo para competir ya que tiene a pepita y papo con mas de 100 de energia"{
		pepita.energia(101)
		carlos.agregarAve(papo)
		
		assert.that(carlos.estaListoParaCompetir())
	}
	
	test "Pepita aumenta su energia luego de que carlos alimanta al equipo"{
		
// 		Mas acoplados a la implementacion de 'comer' en las aves
//		const energiaAnterior = pepita.energia()
//		
//		carlos.alimentarEquipo(50)
//		
//		assert.equals(pepita.energia(), energiaAnterior + 100)

		const energiaAnterior = pepita.energia()
		
		carlos.alimentarEquipo(50)
		
		assert.that(pepita.energia() > energiaAnterior)
	}
	
	test "Todos los intergantes del equipo aumentan su energia luego de que el equipo coma"{
		
		carlos.agregarAve(pepito)
		
		const energiaAnteriorPepita = pepita.energia()
		const energiaAnteriorPepito = pepito.energia()
		
		carlos.alimentarEquipo(50)
		
		assert.that(pepita.energia() > energiaAnteriorPepita && pepito.energia() > energiaAnteriorPepito)
	}
	
	test "pepita arranca en buenos aires" {
		assert.equals(buenosAires, pepita.ciudadActual())
	}
	
}